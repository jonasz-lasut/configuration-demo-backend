set shell := ["bash", "-c"]

app_name := "Todo App"

pg_container := "todo-postgres"
pg_db := "upbound"
pg_user := "todo"
pg_password := "secretpass"

oci_image := "ttl.sh/axum_demo_todo:6h"

setup_db:
  #!/usr/bin/env bash
  docker run --name {{ pg_container }} -e POSTGRES_PASSWORD={{ pg_password }} -e POSTGRES_USER={{ pg_user }} -e POSTGRES_DB={{ pg_db }} -p 5432:5432 --rm -itd postgres:16
  sleep 10
  docker exec -it {{ pg_container }} psql -U {{ pg_user }} -d {{ pg_db }} -c "CREATE TABLE IF NOT EXISTS todos (id SERIAL PRIMARY KEY, note VARCHAR(255) NOT NULL, status BOOLEAN NOT NULL DEFAULT false);"

_connect_db:
  docker exec -it {{ pg_container }} psql -U {{ pg_user }} -d {{ pg_db }}

teardown_db:
  if docker ps | grep -q {{ pg_container }}; then docker stop {{ pg_container }}; fi

start_api:
  POSTGRES_HOST=127.0.0.1 POSTGRES_DB="{{ pg_db }}" POSTGRES_USER="{{ pg_user }}" POSTGRES_PASSWORD="{{ pg_password }}" APPLICATION_NAME="{{ app_name }}" cargo run

generate_css:
  #!/usr/bin/env bash
  cd tailwind
  npm run build-css

build_container_image: generate_css
  pack build {{ oci_image }}

demo_image: build_container_image
  docker push {{ oci_image }}

run: generate_css start_api

setup: setup_db run
